// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // zx: if (zx == 1) set x = 0
    Mux16(a=x, b[0..15]=false, sel=zx, out=zeroX);

    // nx: if (nx == 1) set x = !x
    Not16(in=zeroX, out=notZeroX);
    Mux16(a=zeroX, b=notZeroX, sel=nx, out=negZeroX);

    // zy: if (zy == 1) set y = 0
    Mux16(a=y, b[0..15]=false, sel=zy, out=zeroY);

    // ny: if (ny == 1) set y = !y
    Not16(in=zeroY, out=notZeroY);
    Mux16(a=zeroY, b=notZeroY, sel=ny, out=negZeroY);

    // if (f == 1)  set out = x + y
    Add16(a=negZeroX, b=negZeroY, out=xSUMy);  
    And16(a=negZeroX, b=negZeroY, out=xANDy);
    Mux16(a=xANDy, b=xSUMy, sel=f, out=halfOut);

    // if (no == 1) set out = !out
    Not16(in=halfOut, out=notHalfOut);
    Mux16(a=halfOut, b=notHalfOut, sel=no, out=out, out=outFinal);

    // if (out == 0) set zr = 1
    // TODO: check a more elegant way of solving this one
    Or16(a=outFinal, b=outFinal, out[0..7]=outFinal1, out[8..15]=outFinal2);

    Or8Way(in=outFinal1, out=orOutFinal1);
    Or8Way(in=outFinal2, out=orOutFinal2);

    Or(a=orOutFinal1, b=orOutFinal2, out=notZr);
    Not(in=notZr, out=zr);

    // if (out < 0) set ng = 1
    // TODO: check a more elegant way of solving this one
    Not16(in=outFinal, out[15]=notMSB);
    Not(in=notMSB, out=ng);
}
